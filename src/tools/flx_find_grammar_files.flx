// find grammar files

fun / (s:string, r:string) => Filename::join (s,r);
var arg1 = System::argv 1;
var dir = if arg1 == "" then "build"/"release"/"share"/"lib" else arg1 / "share" / "lib";
println$ "[flx_find_grammar_files] ** Scanning " + dir;

var fsyn = RE2 ".*\\.fsyn";

// FILES IN TOP LEVEL GRAMMAR DIRECTORY
var gfiles = FileSystem::regfilesin (dir, fsyn);
//for g in gfiles perform println$ "  " + g;

var initfsyn =([ "grammar/utility.fsyn", "grammar/grammar_scheme_support.fsyn", "grammar/blocks.fsyn"]);
var termfsyn = (["grammar/felix.fsyn", "grammar/save.fsyn"]);

// OLD LIST
var filesname = dir/"grammar"/"grammar.files";
var oldfile=load filesname;
var oldfiles = filter (fun (x:string)=> x not in ( "" ! "@extras.files" ! (initfsyn + termfsyn))) (split (oldfile,"\n"));

var gfiles2 = filter (fun (x:string) => x not in ("" ! (initfsyn + termfsyn))) gfiles;
var gfiles3 = initfsyn + gfiles2 + termfsyn;

var same = gfiles2 \cong oldfiles;

if same do
  println$ "--- Unchanged " + filesname;
  System::exit 0;
else
  for name in gfiles2 perform
    if name not in oldfiles perform println$ name + " is new";
  for name in oldfiles perform
    if name not in gfiles2 perform println$ name + " is missing";

  println$ "+++ Writing " + filesname;
  save (filesname, gfiles3);
done
 
